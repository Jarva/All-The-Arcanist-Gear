plugins {
    id "dev.architectury.loom" version "1.6-SNAPSHOT"
    id "maven-publish"
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.mod_id
version = "${project.minecraft_version}-${new File('version').text.trim()}"
group = project.mod_group_id

def generatedResources = file("src/generated/resources")
sourceSets {
    main {
        resources.srcDir generatedResources
    }
}

loom {
    runs {
        data {
            data()
            programArgs "--all", "--mod", "allthearcanistgear", "--existing-mod", "ars_nouveau", "--existing-mod", "ars_elemental", "--existing-mod", "allthemodium"
            programArgs "--existing", file("src/main/resources").absolutePath
            programArgs "--output", generatedResources.absolutePath
        }
    }

    forge {
        mixinConfigs = [
            "allthearcanistgear.mixins.json"
        ]
    }
}

repositories {
    mavenLocal() {
        content {
            includeGroup "com.hollingsworth.ars_nouveau"
        }
    }

    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }

    maven { url = 'https://files.minecraftforge.net/maven/' }

    maven {
        url = "https://maven.theillusivec4.top/"
    }
    maven {
        name = "Progwml6 maven"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
    maven {
        url = "https://maven.blamejared.com"
    }
    maven {
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    maven { url 'https://jitpack.io' }

    maven {
        name = 'GeckoLib'
        url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
        content {
            includeGroupByRegex("software\\.bernie.*")
            includeGroup("com.eliotlash.mclib")
        }
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"

    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip")
    }

    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    modImplementation "com.hollingsworth.ars_nouveau:ars_nouveau-${project.minecraft_version}:${project.ars_version}"
    modImplementation "software.bernie.geckolib:geckolib-forge-${project.minecraft_version}:${project.gecko_version}"
    forgeRuntimeLibrary "com.eliotlash.mclib:mclib:20"
    modImplementation "vazkii.patchouli:Patchouli:${project.minecraft_version}-${project.patchouli_version}"

    modCompileOnlyApi "mezz.jei:jei-${project.minecraft_version}-common-api:${project.jei_version}"
    modCompileOnlyApi "mezz.jei:jei-${project.minecraft_version}-forge-api:${project.jei_version}"

    modRuntimeOnly("mezz.jei:jei-${project.minecraft_version}-forge:${project.jei_version}") { transitive = false }

    modImplementation "top.theillusivec4.curios:curios-forge:${project.curios_version}+${project.minecraft_version}"
    modImplementation "com.github.glitchfiend:TerraBlender-forge:${project.minecraft_version}-${project.terrablender_version}"

    implementation(annotationProcessor("com.github.llamalad7.mixinextras:mixinextras-common:0.2.0-beta.8"))
    implementation("com.github.llamalad7.mixinextras:mixinextras-forge:0.2.0-beta.8")
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    modImplementation "curse.maven:allthemodium-364466:5439699"
    modRuntimeOnly "curse.maven:ato-405593:5348605"
    modCompileOnly "curse.maven:ars-elemental-561470:5609198"

    //    runtimeOnly "curse.maven:ars-elemental-561470:5652442"
    //    runtimeOnly "curse.maven:polymorph-388800:5550094"
}

processResources {
    var replaceProperties = [
            minecraft_version      : minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version          : forge_version,
            forge_version_range    : forge_version_range,
            loader_version_range   : loader_version_range,
            mod_id                 : mod_id,
            mod_name               : mod_name,
            mod_license            : mod_license,
            mod_version            : version,
            mod_authors            : mod_authors,
            mod_description        : mod_description
    ]
    inputs.properties replaceProperties

    filesMatching("META-INF/mods.toml") {
        expand replaceProperties
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
    options.release = 17
}

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_authors,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_authors,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
        }
    }

    repositories {}
}
